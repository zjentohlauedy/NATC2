plugins {
	id 'org.springframework.boot' version '2.3.0.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'groovy'
}

group = 'org.natc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '13'
defaultTasks = ['clean', 'build']

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

sourceSets {
	test {
		java {
			srcDirs = ['src/test/unit/java', 'src/test/integration/java']
		}
		groovy {
			srcDirs = ['src/test/functional/groovy']
		}
		resources {
			srcDirs = ['src/test/resources']
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-quartz'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:+'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'io.zonky.test:embedded-database-spring-test:1.+'
	testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:+'
	testImplementation platform('org.spockframework:spock-bom:2.0-M2-groovy-2.5')
	testImplementation 'org.spockframework:spock-core'
	testImplementation 'org.spockframework:spock-spring'
	testImplementation enforcedPlatform('io.zonky.test.postgres:embedded-postgres-binaries-bom:12.1.0')
}

test {
	useJUnitPlatform()
	afterSuite { desc, result ->
		if (!desc.parent)
			println("${result.resultType} " +
					"(${result.testCount} tests, " +
					"${result.successfulTestCount} successful, " +
					"${result.failedTestCount} failures, " +
					"${result.skippedTestCount} skipped)")
	}
}

wrapper {
	gradleVersion = '6.5'
	distributionType = Wrapper.DistributionType.ALL
}
